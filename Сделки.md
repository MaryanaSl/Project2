### Сделки

1. Добавлен регистр накопления **Товары** вида "Остатки"  с измерением Номенклатура (СправочникСсылка.Номенклатура) и ресурсами Сумма (ОпределяемыйТип.Сумма) и Количество (ОпределяемыйТип.Количество)

2. Добавлен регистр накопления **ВзаиморасчетыСКонтрагентами** вида "Остатки" с измерением Контрагент (СправочникСсылка.Контрагенты) и ресурсом Сумма (ОпределяемыйТип.Сумма)

3. Добавлен регистр накопления **Доходы** вида "Обороты" с измерением Номенклатура (СправочникСсылка.Номенклатура) и ресурсами Сумма (ОпределяемыйТип.Сумма) и Количество (ОпределяемыйТип.Количество)

4. Добавлен регистр накопления **Расходы** вида "Обороты" с измерением Номенклатура (СправочникСсылка.Номенклатура) и ресурсом Сумма (ОпределяемыйТип.Сумма)
  
5. Добавлен общий модуль НДСКлиентСервер:
 * С флагами "Клиент" и "Сервер"
 * Создана в нем функция СуммаНДСПоСтавке(Сумма, СтавкаНДС), возвращающая сумму НДС, рассчитанную от суммы по ставке. Чтобы обеспечить работоспособность на клиенте, для получения значений ставок НДС используется функция ПредопределенноеЗначение().

<details>
	
 <summary><u>Общий модуль НДСКлиентСервер (вызывается в процедуре РассчитатьСуммаНДС)</u></summary>
 
```bsl

Функция СуммаНДСПоСтавке(Сумма, СтавкаНДС)Экспорт

	//Создать функцию СуммаНДСПоСтавке(Сумма, СтавкаНДС), 
	//возвращающую сумму НДС, рассчитанную от суммы по ставке согласно требованиям. 
	//Чтобы обеспечить работоспособность на клиенте, 
	//для получения значений ставок НДС используйте функцию ПредопределенноеЗначение()	
	
	
	//НДС рассчитывается по ставкам, определяемым по значению перечисления 
	//СтавкиНДС (БезНДС - 0%, НДС10 - 10%, НДС20 - 20%). 
	//Сумма НДС определяется умножением суммы на ставку 
	//(т.е. НДС рассчитывается по схеме "в том числе", например, 
	//для ставки 20% и суммы 120 р сумма НДС будет равна 120 * 0.2 / (1 + 0.2) = 20.
	
	Если СтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.БезНДС") Тогда
		Ставка = 0;
	ИначеЕсли СтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС10") Тогда
		Ставка = 0.1;
	ИначеЕсли СтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС20") Тогда
		Ставка = 0.2;     
	КонецЕсли;
	
	НДС = Сумма * Ставка / (1 + Ставка);
	
	Возврат НДС;
	
КонецФункции 
```
</details>

6. Добавлен документ **ПоступлениеТоваровИУслуг** с реквизитами Поставщик (СправочникСсылка.Контрагенты) и Сумма (ОпределяемыйТип.Сумма) и ТЧ ТоварыИУслуги с необходимыми реквизитами (Номенклатура, Кол-во, СтавкаНДС, Цена, Сумма, СуммаНДС):
  * В "Движениях" выбраны регистры накопления Товары, Расходы и ВзаиморасчетыСКонтрагентами
  * Создана форму документа, в которой:
     * В таблице товаров и услуг включено отображение подвала и выведен в него итог по колонкам "Сумма" и "Сумма НДС" (флаг "Отображать в подвале" и заполнение пути к данным подвала).
    * Созданы клиентские процедуры:
      * ПриИзмененииКоличества(ИзмененнаяСтрока), ПриИзмененииЦены(ИзмененнаяСтрока), в которых:
        * Рассчитывается сумма по цене и количеству и вызывается ПриИзмененииСуммы()
      * ПриИзмененииСуммы(ИзмененнаяСтрока), ПриИзмененииСтавкиНДС(ИзмененнаяСтрока), в которых:
        * Рассчитывается сумму НДС по сумме и ставка вызовом НДСКлиентСервер.СуммаНДСПоСтавке()
    * Переопределлены обработчики событий ПриИзменении полей ввода для количества, цены, суммы и ставки НДС, и вызываются из них процедуры ПриИзменении<...>, передавая в качестве параметра ТекущиеДанные таблицы.
   
<details>
	
 <summary><u>Модуль формы документа Поступение</u></summary>

```bsl


&НаКлиенте
Процедура ПриИзмененииКоличества(ИзмененнаяСтрока)
	// Рассчитывать сумму по количеству и вызывать ПриИзмененииСуммы()  
	РассчитатьСуммаСтроки (ИзмененнаяСтрока); 
	ПриИзмененииСуммы(ИзмененнаяСтрока);
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииЦены(ИзмененнаяСтрока)
	// Рассчитывать сумму по цене и вызывать ПриИзмененииСуммы() 
    РассчитатьСуммаСтроки (ИзмененнаяСтрока);
	ПриИзмененииСуммы(ИзмененнаяСтрока);
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииСуммы(ИзмененнаяСтрока) 
	
	Если ПустаяСтрока(ИзмененнаяСтрока.Количество) или ПустаяСтрока(ИзмененнаяСтрока.Цена) Тогда
		Возврат;
	Иначе 
		ИзмененнаяСтрока.Цена = ИзмененнаяСтрока.Сумма / ИзмененнаяСтрока.Количество;
	КонецЕсли;
	// Рассчитывать сумму НДС по сумме вызовом НДСКлиентСервер.СуммаНДСПоСтавке() 
	РассчитатьСуммаНДС (ИзмененнаяСтрока);    
	

КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииСтавкиНДС(ИзмененнаяСтрока)
	// Рассчитывать сумму НДС по ставке вызовом НДСКлиентСервер.СуммаНДСПоСтавке() 
    РассчитатьСуммаНДС (ИзмененнаяСтрока);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыИУслугиКоличествоПриИзменении(Элемент)
	// Переопределить обработчики событий ПриИзменении полей ввода для количества, 
	//цены, суммы и ставки НДС, и вызывать из них процедуры ПриИзменении<...>, 
	//передавая в качестве параметра ТекущиеДанные таблицы

    ТекДанные = Элементы.ТоварыИУслуги.ТекущиеДанные;
	  
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПриИзмененииКоличества(ТекДанные);
    
КонецПроцедуры

&НаКлиенте
Процедура ТоварыИУслугиЦенаПриИзменении(Элемент)
	// Переопределить обработчики событий ПриИзменении полей ввода для количества, 
	//цены, суммы и ставки НДС, и вызывать из них процедуры ПриИзменении<...>, 
	//передавая в качестве параметра ТекущиеДанные таблицы

	ТекДанные = Элементы.ТоварыИУслуги.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ПриИзмененииЦены(ТекДанные);  
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыИУслугиСуммаПриИзменении(Элемент)  
	// Переопределить обработчики событий ПриИзменении полей ввода для количества, 
	//цены, суммы и ставки НДС, и вызывать из них процедуры ПриИзменении<...>, 
	//передавая в качестве параметра ТекущиеДанные таблицы

	ТекДанные = Элементы.ТоварыИУслуги.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;    
	ПриИзмененииСуммы(ТекДанные);

КонецПроцедуры

&НаКлиенте
Процедура ТоварыИУслугиСтавкаНДСПриИзменении(Элемент)
	// Переопределить обработчики событий ПриИзменении полей ввода для количества, 
	//цены, суммы и ставки НДС, и вызывать из них процедуры ПриИзменении<...>, 
	//передавая в качестве параметра ТекущиеДанные таблицы

	ТекДанные = Элементы.ТоварыИУслуги.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;    
	ПриИзмененииСтавкиНДС(ТекДанные);

КонецПроцедуры


&НаКлиенте
Процедура РассчитатьСуммаСтроки (ИзмененнаяСтрока)
	//получим значения в колоках текущей строки - свойство табличной части ТекущиеДанные
	//Получим коллекцию. в которой есть все данные по строке
       
	ИзмененнаяСтрока.Сумма = ИзмененнаяСтрока.Количество * ИзмененнаяСтрока.Цена;
КонецПроцедуры  

&НаКлиенте
Процедура РассчитатьСуммаНДС (ИзмененнаяСтрока)
	//получим значения в колоках текущей строки - свойство табличной части ТекущиеДанные
	//Получим коллекцию. в которой есть все данные по строке
	Если ПустаяСтрока(ИзмененнаяСтрока.СтавкаНДС) Тогда
		Возврат;
	Иначе ИзмененнаяСтрока.СуммаНДС = НДСКлиентСервер.СуммаНДСПоСтавке(ИзмененнаяСтрока.Сумма, ИзмененнаяСтрока.СтавкаНДС);
	КонецЕсли;
	
КонецПроцедуры

```
</details>

  * Форма выглядит так:
![image](https://github.com/user-attachments/assets/802d27cd-7879-46f1-bcb2-ba6da69c9319)

  * В модуле объекта:
    * Создан обработчик события ОбработкаПроведения и формируются движения:
      * По регистру ВзаиморасчетыСКонтрагентами - одно движение вида "Расход" с указанием контрагента-поставщика и общей суммы
      * По регистру Товары - движения вида "Приход" по каждой строке с номенклатурой типа Товары с указанием номенклатуры, количества и суммы
      * По регистру Расходы - движения по каждой строке с номенклатурой типа Услуги с указанием номенклатуры и суммы
    * Создан обработчик события ПередЗаписью и сохраняется в реквизит шапки Сумма итог по одноименному реквизиту табличной части для отображения в списках

<details>
	
 <summary><u>Модуль объекта документа Поступение</u></summary>

```bsl 
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	Ответственный = ПараметрыСеанса.ТекущийСотрудник;
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения) 
	СуммаДокумента = ТоварыИУслуги.Итог("Сумма"); 
	//товары - название табл.части, итог - команда. "Сумма" - название колонки
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	//формировать движения (выбрав предварительно запросом табличную часть с типами номенклатуры):  
	Запрос = Новый Запрос;
	Запрос.Текст = 
			"ВЫБРАТЬ
			|	ПоступлениеТоваровИУслугТоварыИУслуги.Номенклатура.ТипНоменклатуры КАК НоменклатураТипНоменклатуры,
			|	ПоступлениеТоваровИУслугТоварыИУслуги.Номенклатура КАК Номенклатура,
			|	ПоступлениеТоваровИУслугТоварыИУслуги.Количество КАК Количество,
			|	ПоступлениеТоваровИУслугТоварыИУслуги.Сумма КАК Сумма
			|ИЗ
			|	Документ.ПоступлениеТоваровИУслуг.ТоварыИУслуги КАК ПоступлениеТоваровИУслугТоварыИУслуги
			|ГДЕ
			|	ПоступлениеТоваровИУслугТоварыИУслуги.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ЭтотОбъект.Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Сообщить("Выберете товары или услуги");  
		Отказ = Истина;
	КонецЕсли;
	
	ТабличнаяЧасть = РезультатЗапроса.Выгрузить();
	
	
	//По регистру ВзаиморасчетыСКонтрагентами - 
	//одно движение вида "Расход" с указанием контрагента-поставщика и общей суммы  
	Движения.ВзаиморасчетыСКонтрагентами.Очистить();
	Движения.ВзаиморасчетыСКонтрагентами.Записывать = Истина;
	
	Движение = Движения.ВзаиморасчетыСКонтрагентами.Добавить();
	Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
	Движение.Период = Дата;
	Движение.Контрагент = Поставщик;
	Движение.Сумма = СуммаДокумента;
	
	//По регистру Товары - движения вида "Приход" по каждой строке с 
	//номенклатурой типа Товары с указанием номенклатуры, количества и суммы
	
	Движения.Товары.Очистить();
	Движения.Товары.Записывать = Истина;
	
	Движения.Расходы.Очистить();
	Движения.Расходы.Записывать = Истина;
	
	ТипТовар = Перечисления.ТипыНоменклатуры.Товар;
	
	Для каждого Строка из ТабличнаяЧасть Цикл
		
		Если Строка.Номенклатура.ТипНоменклатуры = ТипТовар Тогда
			
			Движение = Движения.Товары.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
			Движение.Период = Дата;
			Движение.Номенклатура = Строка.Номенклатура;
			Движение.Количество = Строка.Количество;
			Движение.Сумма = Строка.Сумма;
			
		Иначе  
			//По регистру Расходы - движения по каждой строке с 
			//номенклатурой типа Услуги с указанием номенклатуры и суммы
			Движение = Движения.Расходы.Добавить();
			Движение.Период = Дата;
			Движение.Номенклатура = Строка.Номенклатура;
			Движение.Сумма = Строка.Сумма;   
			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

```
</details>

 
 * В итоге в документе Поступление:
  * При изменении количества и цены пересчитывается сумма и сумма НДС, а при изменении суммы и ставки НДС - сумма НДС
  * Формируются движения по трем регистрам накопления, и сумма движения по Взаиморасчетам равна итогу по колонке Сумма и сумме движений по Товарам и Расходам. Пример движений для документа со снимка выше:
![image](https://github.com/user-attachments/assets/abd6dce4-f4e1-465f-be2c-06e41b266405)
![image](https://github.com/user-attachments/assets/3aa1b372-792f-4376-a1c7-46043c831d5b)
![image](https://github.com/user-attachments/assets/6a32e3da-b269-48d7-aecb-9afe6105a150)

7. Добавлен документ **РеализацияТоваровИУслуг** с реквизитами Покупатель (СправочникСсылка.Контрагенты) и Сумма (ОпределяемыйТип.Сумма) и ТЧ ТоварыИУслуги с необходимыми реквизитами (номенклатура, кол-во, ставкаНДС, Скидка, Цена, Сумма, СуммаНДС):
  * В "Движениях" выбраны регистры накопления Товары, Доходы, Расходы и ВзаиморасчетыСКонтрагентами
  * Создана форма документа, в которой:
    * В таблице товаров и услуг включено отображение подвала и выведен в него итог по колонкам "Сумма" и "Сумма НДС".
    * Создана клиентская функция СуммаПоСтроке(Строка), которая возвращает сумму с учетом количества, цены и скидки 
    * Созданы клиентские процедуры:
      * ПриИзмененииНоменклатуры(ИзмененнаяСтрока), в которой:
        * Заполняется цена и скидка аналогично документам **УстановкаЦен** и **УстановкаСкидок**, а также заполняется ставка НДС и вызываются процедуры ПриИзмененииЦены, ПриИзмененииСкидки и ПриИзмененииСтавкиНДС
      * ПриИзмененииКоличества(ИзмененнаяСтрока), ПриИзмененииЦены(ИзмененнаяСтрока), ПриИзмененииСкидки(ИзмененнаяСтрока), в которых:
        * Рассчитываеся сумма вызовом СуммаПоСтроке() и вызывается ПриИзмененииСуммы()
      * ПриИзмененииСуммы(ИзмененнаяСтрока), ПриИзмененииСтавкиНДС(ИзмененнаяСтрока), в которых:
        * Рассчитывается сумма НДС по сумме и ставке вызовом НДСКлиентСервер.СуммаНДСПоСтавке()
    * Переопределены обработчики событий ПриИзменении полей ввода номенклатуры, количества, цены, скидки, суммы и ставки НДС, и вызываются из них процедуры ПриИзменении<...>, передавая в качестве параметра ТекущиеДанные таблицы.
    * Реализован подбор аналогично документу **УстановкаЦен**, передавая в качестве владельца открываемой формы таблицу ТоварыИУслуги, а в обработчике события ОбработкаВыбора вызывается процедура ПриИзмененииНоменклатуры, чтобы обеспечить получение цен и скидок и автоматический пересчет сумм.


<details>
	
 <summary><u>Модуль формы документа Реализация</u></summary>

```bsl 

//Скидки определяются по срезу последних регистра сведений Скидки. 
//Если скидка установлена и на конкретный элемент справочника Номенклатура, 
//и на номенклатурную группу, приоритет имеет скидка для конкретного элемента.

//Цена определяется по данным регистра сведений Цены и не пересчитывается 
//при изменении скидки. Сумма определяется по цене с учетом скидки как: 
//Сумма = Количество * Цена * (100 - Скидка) / 100 
//При изменении суммы изменяется скидка, но не цена, по обратной формуле: 
//Скидка = 100 * (1 - Сумма / Количество / Цена)     


&НаКлиенте
Функция СуммаПоСтроке(Строка)
	// возвращает сумму с учетом количества, цены и скидки 
	Строка.Сумма = Строка.Количество * Строка.Цена * (100 - Строка.Скидка) / 100;
	Возврат Строка.Сумма;  
	
КонецФункции


&НаКлиенте
Процедура ПриИзмененииНоменклатуры(ИзмененнаяСтрока)
	// Заполнять цену и скидку аналогично документам 
	//УстановкаЦен и УстановкаСкидок, а также заполнять ставку НДС 
	//и вызывать процедуры ПриИзмененииЦены, ПриИзмененииСкидки и ПриИзмененииСтавкиНДС  
	
	ЗаполнитьЦенуИСкидку (ИзмененнаяСтрока);
	ИзмененнаяСтрока.СтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС20");
	
	ПриИзмененииЦены(ИзмененнаяСтрока); 
	ПриИзмененииСкидки(ИзмененнаяСтрока);
	ПриИзмененииСтавкиНДС(ИзмененнаяСтрока);
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьЦенуИСкидку (ИзмененнаяСтрока)
	 
	//вызвать ЦеныВызовСервера.ЦенаНаДату и заполнить цену
	
	Если ЗначениеЗаполнено(ИзмененнаяСтрока.Номенклатура) Тогда
		ИзмененнаяСтрока.Цена = ЦеныВызовСервера.ЦенаНаДату(ИзмененнаяСтрока.Номенклатура, Объект.Дата); 
		ИзмененнаяСтрока.Скидка = ЦеныВызовСервера.СкидкаНаДату(ИзмененнаяСтрока.Номенклатура, Объект.Дата);
	КонецЕсли;  
	
	//Если Не ЗначениеЗаполнено(ИзмененнаяСтрока.Номенклатура) Тогда
	//	Шаблон = "По позиции ""%1"" цена ранее установлена не была";
	//	Сообщение = СтрШаблон(Шаблон, ИзмененнаяСтрока.Номенклатура);
	//	Сообщить (Сообщение);
	//КонецЕсли; 
	
КонецПроцедуры


&НаКлиенте
Процедура ПриИзмененииКоличества(ИзмененнаяСтрока)
	// Рассчитывать сумму вызовом СуммаПоСтроке() и вызывать ПриИзмененииСуммы()  
	СуммаПоСтроке(ИзмененнаяСтрока); 
	ПриИзмененииСуммы(ИзмененнаяСтрока);
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииЦены(ИзмененнаяСтрока)
	// Рассчитывать сумму вызовом СуммаПоСтроке() и вызывать ПриИзмененииСуммы() 
    СуммаПоСтроке(ИзмененнаяСтрока);
	ПриИзмененииСуммы(ИзмененнаяСтрока);
КонецПроцедуры  

&НаКлиенте
Процедура ПриИзмененииСкидки(ИзмененнаяСтрока)
	// Рассчитывать сумму вызовом СуммаПоСтроке() и вызывать ПриИзмененииСуммы()
	СуммаПоСтроке(ИзмененнаяСтрока);
	ПриИзмененииСуммы(ИзмененнаяСтрока);
КонецПроцедуры


&НаКлиенте
Процедура ПриИзмененииСуммы(ИзмененнаяСтрока)
	// Рассчитывать сумму НДС по сумме и ставке вызовом НДСКлиентСервер.СуммаНДСПоСтавке() 
	РассчитатьСуммаНДС (ИзмененнаяСтрока); 
	Если ИзмененнаяСтрока.Количество=0 или ИзмененнаяСтрока.Цена=0 Тогда
		Возврат;
	Иначе 
		ИзмененнаяСтрока.Скидка = 100 * (1 - ИзмененнаяСтрока.Сумма / ИзмененнаяСтрока.Количество / ИзмененнаяСтрока.Цена)   
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииСтавкиНДС(ИзмененнаяСтрока)
	// Рассчитывать сумму НДС по сумме и ставке вызовом НДСКлиентСервер.СуммаНДСПоСтавке() 
    РассчитатьСуммаНДС (ИзмененнаяСтрока);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыИУслугиКоличествоПриИзменении(Элемент)
	// Переопределить обработчики событий ПриИзменении полей ввода номенклатуры, 
	//количества, цены, скидки, суммы и ставки НДС, и вызывать из них процедуры 
	//ПриИзменении<...>, передавая в качестве параметра ТекущиеДанные таблицы.
    ТекДанные = Элементы.ТоварыИУслуги.ТекущиеДанные;
	  
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПриИзмененииКоличества(ТекДанные);
    
КонецПроцедуры

&НаКлиенте
Процедура ТоварыИУслугиЦенаПриИзменении(Элемент)
	// Переопределить обработчики событий ПриИзменении полей ввода номенклатуры, 
	//количества, цены, скидки, суммы и ставки НДС, и вызывать из них процедуры 
	//ПриИзменении<...>, передавая в качестве параметра ТекущиеДанные таблицы.
	
	ТекДанные = Элементы.ТоварыИУслуги.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ПриИзмененииЦены(ТекДанные);  
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыИУслугиСуммаПриИзменении(Элемент)  
	/// Переопределить обработчики событий ПриИзменении полей ввода номенклатуры, 
	//количества, цены, скидки, суммы и ставки НДС, и вызывать из них процедуры 
	//ПриИзменении<...>, передавая в качестве параметра ТекущиеДанные таблицы.
	
	ТекДанные = Элементы.ТоварыИУслуги.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;    
	ПриИзмененииСуммы(ТекДанные);

КонецПроцедуры

&НаКлиенте
Процедура ТоварыИУслугиСтавкаНДСПриИзменении(Элемент)
	// Переопределить обработчики событий ПриИзменении полей ввода номенклатуры, 
	//количества, цены, скидки, суммы и ставки НДС, и вызывать из них процедуры 
	//ПриИзменении<...>, передавая в качестве параметра ТекущиеДанные таблицы.
	
	ТекДанные = Элементы.ТоварыИУслуги.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;    
	ПриИзмененииСтавкиНДС(ТекДанные);

КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммаНДС (ИзмененнаяСтрока)
	//получим значения в колоках текущей строки - свойство табличной части ТекущиеДанные
	//Получим коллекцию. в которой есть все данные по строке
	Если ПустаяСтрока(ИзмененнаяСтрока.СтавкаНДС) Тогда
		Возврат;
	Иначе ИзмененнаяСтрока.СуммаНДС = НДСКлиентСервер.СуммаНДСПоСтавке(ИзмененнаяСтрока.Сумма, ИзмененнаяСтрока.СтавкаНДС);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыИУслугиНоменклатураПриИзменении(Элемент)
	// Переопределить обработчики событий ПриИзменении полей ввода номенклатуры, 
	//количества, цены, скидки, суммы и ставки НДС, и вызывать из них процедуры 
	//ПриИзменении<...>, передавая в качестве параметра ТекущиеДанные таблицы.
    ТекДанные = Элементы.ТоварыИУслуги.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;    
	ПриИзмененииНоменклатуры(ТекДанные);

КонецПроцедуры

&НаКлиенте
Процедура ТоварыИУслугиСкидкаПриИзменении(Элемент)
	// Переопределить обработчики событий ПриИзменении полей ввода номенклатуры, 
	//количества, цены, скидки, суммы и ставки НДС, и вызывать из них процедуры 
	//ПриИзменении<...>, передавая в качестве параметра ТекущиеДанные таблицы.  
	ТекДанные = Элементы.ТоварыИУслуги.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;    
	ПриИзмененииСкидки(ТекДанные);

КонецПроцедуры

&НаКлиенте
Процедура Подбор(Команда)
	// Добавить команду Подбор, разместив ее в командной панели таблицы цен. 
	//В обработчике команды открыть форму выбора справочника Номенклатура 
	//с параметром ЗакрыватьПриВыборе = Ложь, указав в качестве владельца таблицу ТоварыИУслуги. 
	//а в обработчике события ОбработкаВыбора вызывая процедуру ПриИзмененииНоменклатуры, 
	//чтобы обеспечить получение цен и скидок и автоматический пересчет сумм.

    ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ВыборГруппИЭлементов", ИспользованиеГруппИЭлементов.ГруппыИЭлементы);
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", Ложь);
	ОткрытьФорму("Справочник.Номенклатура.ФормаВыбора", ПараметрыФормы, Элементы.ТоварыИУслуги);

КонецПроцедуры

&НаКлиенте
Процедура ТоварыИУслугиОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	//Переопределить событие ОбработкаВыбора таблицы товары и услуги. 
	//В обработчике отказаться от стандартной обработки и, 
	//если в таблице еще нет выбранного значения - добавить строку и 
	//вызвать процедуру ПриИзмененииНоменклатуры, передав добавленную строку.  
	СтандартнаяОбработка = Ложь;
	
	// Выполним поиск в таблице строки с этой номенклатурой;
	ОтборСтрок = Новый Структура ("Номенклатура", ВыбранноеЗначение);
	НайденныеСтрокиВСпискеВыбранных = Объект.ТоварыИУслуги.НайтиСтроки(ОтборСтрок);
	
	
	//Если строка не найдена - создадим новую строку, в колонку 
	//Номенклатура укажем выбранное значение, 	
	Если НайденныеСтрокиВСпискеВыбранных.Количество() = 0 Тогда
		
		НоваяСтрокаТаблицы = Объект.ТоварыИУслуги.Добавить();
		НоваяСтрокаТаблицы.Номенклатура = ВыбранноеЗначение;     
		ПриИзмененииНоменклатуры(НоваяСтрокаТаблицы);
		
	Иначе 
		Возврат;
	КонецЕсли;
	
КонецПроцедуры
```
</details>

  * Форма выглядит так:
![image](https://github.com/user-attachments/assets/ef1192f9-e8b3-43d5-91f4-bafac8301e13)

  * В модуле объекта:
    * Создан обработчик события ОбработкаПроведения и формируются движения:
      * По регистру ВзаиморасчетыСКонтрагентами - одно движение вида "Приход" с указанием контрагента-покупателя и общей суммы
      * По регистру Товары - движения вида "Расход" по каждой строке с номенклатурой типа Товары с указанием номенклатуры, количества и суммы. Сумму рассчитывать, определив среднюю стоимость единицы делением суммы остатка на количество остатка и умножив среднюю стоимость на реализуемое количество. При нехватке остатков отказываться от проведения, выводя пользователю разумное сообщение.
      * По регистру Расходы - движения по каждой строке с номенклатурой типа Товары с указанием номенклатуры и суммы, равной сумме расхода по регистру Товары.
      * По регистру Доходы - движения по каждой строке с указанием номенклатуры, количества и суммы
    * Создан обработчик события ПередЗаписью и сохраняются в реквизит шапки Сумма итог по одноименному реквизиту табличной части для отображения в списках
   
<details>
	
 <summary><u>Модуль объекта документа с контролем остатков (в связи с тем, что на данном этапе еще не проходили виртуальные таблицы остатков и оборотов, обращение происходит к остаткам регистра накопления Товары,а не к виртуальной таблице Остатки)</u></summary>

 ```bsl
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	Ответственный = ПараметрыСеанса.ТекущийСотрудник;
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения) 
	СуммаДокумента = ТоварыИУслуги.Итог("Сумма"); 
	//товары - название табл.части, итог - команда. "Сумма" - название колонки
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	//формировать движения, выбрав предварительно запросом данные табличной части документа 
	//с типами номенклатуры и соединив с виртуальной таблицей Остатки регистра Товары по номенклатуре:  
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	РеализацияТоваровИУслугТоварыИУслуги.Номенклатура.ТипНоменклатуры КАК НоменклатураТипНоменклатуры,
	               |	РеализацияТоваровИУслугТоварыИУслуги.Номенклатура КАК Номенклатура,
	               |	СУММА(РеализацияТоваровИУслугТоварыИУслуги.Количество) КАК Количество,
	               |	СУММА(РеализацияТоваровИУслугТоварыИУслуги.Сумма) КАК Сумма
	               |ПОМЕСТИТЬ ВТ_ДанныеДокумента
	               |ИЗ
	               |	Документ.РеализацияТоваровИУслуг.ТоварыИУслуги КАК РеализацияТоваровИУслугТоварыИУслуги
	               |ГДЕ
	               |	РеализацияТоваровИУслугТоварыИУслуги.Ссылка = &Ссылка
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	РеализацияТоваровИУслугТоварыИУслуги.Номенклатура.ТипНоменклатуры,
	               |	РеализацияТоваровИУслугТоварыИУслуги.Номенклатура
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_ДанныеДокумента.НоменклатураТипНоменклатуры КАК НоменклатураТипНоменклатуры,
	               |	ВТ_ДанныеДокумента.Номенклатура КАК Номенклатура,
	               |	ВТ_ДанныеДокумента.Количество КАК Количество,
	               |	ВТ_ДанныеДокумента.Сумма КАК Сумма,
	               |	ЕСТЬNULL(ТоварыОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
	               |	ЕСТЬNULL(ТоварыОстатки.СуммаОстаток, 0) КАК СуммаОстаток
	               |ИЗ
	               |	ВТ_ДанныеДокумента КАК ВТ_ДанныеДокумента
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.Товары.Остатки(
	               |				&Период,
	               |				Номенклатура В
	               |					(ВЫБРАТЬ
	               |						ВТ_ДанныеДокумента.Номенклатура КАК Номенклатура
	               |					ИЗ
	               |						ВТ_ДанныеДокумента КАК ВТ_ДанныеДокумента)) КАК ТоварыОстатки
	               |		ПО ВТ_ДанныеДокумента.Номенклатура = ТоварыОстатки.Номенклатура";
	
	Запрос.УстановитьПараметр("Ссылка", ЭтотОбъект.Ссылка);
	//установим параметр Период - прямо до проведения документа 
	Запрос.УстановитьПараметр("Период", Новый Граница (МоментВремени(), ВидГраницы.Исключая));
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	//По регистру ВзаиморасчетыСКонтрагентами - 
	//одно движение вида "Приход" с указанием контрагента-покупателя и общей суммы
	
	Движения.ВзаиморасчетыСКонтрагентами.Очистить();
	Движения.ВзаиморасчетыСКонтрагентами.Записывать = Истина;
	
	
	Движение = Движения.ВзаиморасчетыСКонтрагентами.Добавить();
	Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
	Движение.Период = Дата;
	Движение.Контрагент = Покупатель;
	Движение.Сумма = СуммаДокумента;
	
	//По регистру Товары - движения вида "Расход" по каждой строке с номенклатурой 
	//типа Товары с указанием номенклатуры, количества и суммы. 
	//Сумму рассчитывать, определив среднюю стоимость единицы делением суммы 
	//остатка на количество остатка и умножив среднюю стоимость на реализуемое количество. 
	//При нехватке остатков отказываться от проведения, выводя пользователю разумное сообщение. 
	
	Движения.Товары.Очистить();
	Движения.Товары.Записывать = Истина;
	
	Движения.Доходы.Очистить();
	Движения.Доходы.Записывать = Истина;  
	
	Движения.Расходы.Очистить();
	Движения.Расходы.Записывать = Истина;
	
	//По регистру Доходы - движения по каждой строке с указанием номенклатуры, количества и суммы
	Пока Выборка.Следующий() Цикл 
		Движение = Движения.Доходы.Добавить();
		Движение.Период = Дата;
		Движение.Номенклатура = Выборка.Номенклатура;
		Движение.Количество = Выборка.Количество;
		Движение.Сумма = Выборка.Сумма;
		
		Если Выборка.НоменклатураТипНоменклатуры <> Перечисления.ТипыНоменклатуры.Товар Тогда
			Продолжить;
		КонецЕсли;
		
		//проверяем, хватает ли остатка товара
		Если Выборка.Количество > Выборка.КоличествоОстаток Тогда
			//сообщаем что товара не хватате
			Сообщить (СтрШаблон("Не хватает товара %1", Выборка.Номенклатура));
			Отказ = Истина;
			//прекращаем выполнение этой итерации цикла
			Продолжить;
		КонецЕсли;
		
		//необходмо рассчитать себестоимось    
		//Если кол-во остаток = кол-ву в документе, т.е. списываем все в 0, то СС = Сумма Остаток
		Если Выборка.Количество = Выборка.КоличествоОстаток Тогда 
			Себестоимость = Выборка.СуммаОстаток;
		ИНаче
			Себестоимость = Выборка.СуммаОстаток * Выборка.Количество/Выборка.КоличествоОстаток;
		КонецЕсли;
		
		//формируем движения в регистре Товары
		Движение = Движения.Товары.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Номенклатура = Выборка.Номенклатура;
		Движение.Количество = Выборка.Количество; 
		Движение.Сумма = Себестоимость;
		
		//По регистру Расходы - движения по каждой строке с номенклатурой типа Товары 
		//с указанием номенклатуры и суммы, равной сумме расхода по регистру Товары.
		Движение = Движения.Расходы.Добавить();
		Движение.Период = Дата;
		Движение.Номенклатура = Выборка.Номенклатура;
		Движение.Сумма = Себестоимость;
		
	КонецЦикла;
	
КонецПроцедуры

```
</details>

 * В итоге в документе:
  * При изменении количества, цены и скидки пересчитывается сумма и сумма НДС, а при изменении суммы и ставки НДС - сумма НДС
  * Формируются движения по четырем регистрам накопления, и сумма движения по Взаиморасчетам равна итогу по колонке Сумма и сумме движений по Доходам, а суммы движений по Расходам и Товарам совпадают. Пример движений для документа со снимка выше:
![image](https://github.com/user-attachments/assets/14d91f97-3da0-4d18-afa7-b9e63ec3916a)
![image](https://github.com/user-attachments/assets/de213e41-f053-4de4-b70f-4edc90bb485f)
![image](https://github.com/user-attachments/assets/ea70178c-a927-436b-a719-b43a24d63e91)
![image](https://github.com/user-attachments/assets/688f4b1d-efd5-483e-9d3c-6e8da67c5020)

8. Добавлен журнал документов **Сделки**:
  * В качестве регистрируемых выбраны документы **ПоступлениеТоваровИУслуг** и **РеализацияТоваровИУслуг**
  * Добавлены графы Контрагент  (Поставщик из Поступления и Покупатель из Реализации), Ответственный и Сумма
![image](https://github.com/user-attachments/assets/fd0739f3-0328-4482-8553-9ee0f3b29c5e)
