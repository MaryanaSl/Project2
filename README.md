Итоговая работа блока А «Настройки и справочники»
Описание задачи
Создать конфигурацию «Управление ИТ-фирмой» с базовым набором справочников, поддерживающую управление пользователями ИБ в режиме Предприятия.

Требования к результату
Информационная база должна содержать следующие элементы и особенности:

Перечисление «ЮридическоеФизическоеЛицо».

Справочник «Контрагенты» с определенными реквизитами и формой элемента, удовлетворяющей следующим условиям:

КПП должен быть доступен только для контрагентов — юридических лиц. Доступность должна отрабатываться как при изменении типа, так и при перечитывании элемента справочника из ИБ.
При изменении наименования "полное наименование" должно заполняться с разворачиванием распространённых сокращений организационно-правовых форм. Например, АО «Вектор» должно превращаться в Акционерное общество «Вектор».
Должна быть реализована проверка корректности ИНН как для физических, так и для юридических лиц. Проверять нужно и при попытке записи — с выдачей предупреждения и отказом, и при изменении ИНН — с подсветкой поля ввода или выводом текста ошибки рядом с ним.
В форме списка должны присутствовать все существенные реквизиты в разумном порядке.
В модуле объекта определено заполнение по умолчанию:
Тип — юридическое лицо.
Ответственный — текущий сотрудник из параметра сеанса «ТекущийСотрудник.
Перечисление «Пол».

Справочник «Сотрудники» с реквизитами и модулем менеджера, в котором переопределено получение формы объекта в зависимости от права доступа «Администрирование».

С формой элемента «ФормаАдминистратора»:

которая открывается для пользователей с правом доступа «Администрирование»;

в которой, помимо реквизитов сотрудника, есть флажок «Вход разрешён» и поля ввода «Имя для входа» и «Пароль», отражающие свойства пользователя ИБ.

При записи сотрудника из этой формы при необходимости должно выполняться:

создание пользователя ИБ с ролью «БазовыеПрава»;

изменение его пароля и имени для входа;

при снятии флажка — отключение стандартной аутентификации.

Рядом с полем ввода пароля должна быть команда «Случайный пароль», генерирующая случайный пароль и показывающая его пользователю.

С формой элемента «ФормаПользователя», которая открывается для пользователей без права доступа «Администрирование» и содержит элементы управления для реквизитов сотрудника, упорядоченные по смыслу.

Константа и функциональная опция «ВестиРасчётЗарплаты».
Константа не должна присутствовать в командном интерфейсе сама по себе — флажок «Использовать стандартные команды» должен быть снят.
В состав ФО должны входить реквизиты, относящиеся к расчету зарплаты, справочника «Сотрудники», а константа должна присутствовать флажком в общей форме «НастройкаПрограммы».
Общая форма и общая команда «НастройкаПрограммы».
Форма должна содержать основной реквизит типа «НаборКонстант» и поле флажка для константы «ВестиРасчётЗарплаты».
Общая команда должна открывать общую форму, принадлежать подсистеме «Настройки» и присутствовать в командном интерфейсе раздела «Настройки».
Параметр сеанса «ТекущийСотрудник» - Тип «СправочникСсылка.Сотрудники». Должен заполняться элементом справочника «Сотрудники», идентификатор пользователя ИБ которого совпадает с идентификатором текущего пользователя ИБ.

Роли «БазовыеПрава» и «ПолныеПрава». Роль «ПолныеПрава» должна давать права на всё, кроме интерактивного удаления элементов справочников. Роль «БазовыеПрава» должна давать права на чтение, просмотр и ввод по строке всех данных. Редактирование, добавление и изменение разрешается только для справочника «Контрагенты». Роль не должна давать права на открытие настроек программы.

Подсистема «Настройки»». Содержащая все справочники и общую команду «НастройкаПрограммы».

Процесс выполнения
Весь проект можно найти в репозитории, ниже покажу на мой взгляд, интересные для меня задачи:

В форме элемента справочника Контрагенты реализовать следующее:
При изменении наименования "полное наименование" должно заполняться с разворачиванием распространённых сокращений организационно-правовых форм. Например, АО «Вектор» должно превращаться в Акционерное общество «Вектор».
Для решения данной задачив форме создала доп. реквизит с типом Булево "ПолноеНаименованиеМенялосьВручную". image Ниже приведу часть кода из модуля формы элемента справочника Контрагенты:

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект) //при открытии карточки контрагента
	
	//реализуем управление видимостью поля КПП в зависимости от типа контрагента
	//физ лицо - кпп - скрыть
	
	УстановитьВидимостьКПП(Объект.ЮридическоеФизическоеЛицо, ПредопределенноеЗначение("Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо"), Элементы.КПП.Видимость);
	
	//при чтении формы получить полное наименование из краткого
	//и сверить его с полным наименование на форме 
	//если результат при чтении формы не равен тому, что уже есть на форме,
	//то в невидимом булевском реквизите формы "Полное наименование менялось вручную"
	//указать истину, т.е. это поле менялось вручную 
	
	ПроверкаПолногоНаименования =  СформироватьПолноеНаименование(Объект.Наименование);
	
	Если Врег(ПроверкаПолногоНаименования) = Врег(Объект.ПолноеНаименование) Тогда
		ПолноеНаименованиеМенялосьВручную = Ложь;
	Иначе
		ПолноеНаименованиеМенялосьВручную = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент) 
	
	//реализовать поиск распространённых сокращений организационно-правовых форм в начале строки
	//и заполнение полного наименования по краткому с заменой сокращения ОПФ на её полное наименование
	
	//Не изменять полное наименование, если оно уже было изменено пользователем вручную
	//Чтобы это реализовать, при чтении формы получайте полное наименование из краткого
	
	Если ПолноеНаименованиеМенялосьВручную = Ложь Тогда
		Объект.ПолноеНаименование = СформироватьПолноеНаименование(Объект.Наименование); 
	ИначеЕсли ПолноеНаименованиеМенялосьВручную = Истина Тогда
		
		Возврат;
		
	КонецЕсли;
	
КонецПроцедуры


&НаКлиентеНаСервереБезКонтекста
Функция СформироватьПолноеНаименование(КраткоеНаименование)
	
	//реализовать поиск распространённых сокращений организационно-правовых форм в начале строки
	//и заполнение полного наименования по краткому с заменой сокращения ОПФ на её полное наименование
	
	//создадим соответствия. Где Ключ - сокращение ОПФ. Значение - полное название ОПФ
	АббревиатурыОрганизационноПравовыхФорм = Новый Соответствие;
	АббревиатурыОрганизационноПравовыхФорм.Вставить("ООО", "Общество с Ограниченной Ответственностью");
	АббревиатурыОрганизационноПравовыхФорм.Вставить("ОАО", "Открытое Акционерное Общество");
	АббревиатурыОрганизационноПравовыхФорм.Вставить("ПАО", "Публичное Акционерное Общество");
	АббревиатурыОрганизационноПравовыхФорм.Вставить("НАО", "Непубличное Акцианерное Общество");
	АббревиатурыОрганизационноПравовыхФорм.Вставить("ИП", "Индивидуальный Предприниматель");
	АббревиатурыОрганизационноПравовыхФорм.Вставить("ЗАО", "Закрытое Акционерное Общество"); 
	АббревиатурыОрганизационноПравовыхФорм.Вставить("АО", "Акционерное Общество");
	
	//получим массив слов из Краткого наименования
	МассивСловКраткогоНаименования = СтрРазделить(КраткоеНаименование, " "); 
	
	//пройдемся в цикле по всем словам массива 
	Для К=0 по МассивСловКраткогоНаименования.ВГраница() Цикл
		
		//примем, что слово из массива - ключ дл соотвествия
		КлючДляСоотвествия = Врег(МассивСловКраткогоНаименования[К]);
		
		//получим значение из соотвествия по ключу
		ЗначениеИзСоотвествия = АббревиатурыОрганизационноПравовыхФорм[КлючДляСоотвествия];
		
		Если Не ЗначениеИзСоотвествия = Неопределено Тогда 
			//слово из краткого наименования = значени из соотвествия (полная расшифрова ОПФ)
			МассивСловКраткогоНаименования[К] = ЗначениеИзСоотвествия;
			
			Прервать; //нет смысла дальше искать ОПФ
			
		КонецЕсли;
		
	КонецЦикла;
	
	ПолноеНаименование = СтрСоединить(МассивСловКраткогоНаименования, " ");
	
	Возврат  ПолноеНаименование;
	
КонецФункции
реализация проверки корректности ИНН как для физических, так и для юридических лиц. Проверять нужно и при попытке записи — с выдачей предупреждения и отказом, и при изменении ИНН — с подсветкой поля ввода или выводом текста ошибки рядом с ним. Ниже часть кода из модуля формы элемента справочника Контрагенты:
&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи) 
	
	//реализуем функцию корректности ИНН - отказ от записи при неверном ИНН и выводом предупреждения
	//создать общий модуль РаботаСИНН с флажками Клиент, Сервер, Внешнее соедение  и организовать экспортную функцию
	
	ТекстОшибки = "";
	Если Не РаботаСИНН.ИННВерен(Объект.ИНН, Объект.ЮридическоеФизическоеЛицо, ТекстОшибки) Тогда
		Отказ = Истина;        
		Сообщить (ТекстОшибки);
	КонецЕсли; 
КонецПроцедуры


&НаКлиенте
Процедура ИННПриИзменении(Элемент)
	
	//реализуем функцию корректности ИНН - с выводом рядом с полем ввода тектового описания ошибки в виде декорации
	//создать общий модуль РаботаСИНН с флажками Клиент, Сервер, Внешнее соедение  и организовать экспортную функцию
	//Здесь ТекстОшибки - элемент формы. декорация
	ТекстОшибки = "";
	Если РаботаСИНН.ИННВерен(Объект.ИНН, Объект.ЮридическоеФизическоеЛицо,ТекстОшибки) Тогда
		Элементы.ТекстОшибки.Видимость = Ложь;
		Элементы.ИНН.ЦветТекста = Новый Цвет;		
	Иначе   
		Элементы.ТекстОшибки.Видимость = Истина;
		Элементы.ТекстОшибки.Заголовок = ТекстОшибки;
		Элементы.ИНН.ЦветТекста = WebЦвета.КрасноФиолетовый;
		
	КонецЕсли;
КонецПроцедуры
Из модуля формы перед записью элемента, и при изменении ИНН переходим в общий модуль Работа с ИНН, где проходит проверка ИНН на заполненность, длину ИНН, запрещенные символы, контрольного разряда для разного типа контрагентов

  Функция ИННВерен (ИНН, ТипКонтрагента, ТекстОшибки) Экспорт
	  
	  //проверка длины ИНН
	  //пустой ИНН корректен
	  Если НЕ ЗначениеЗаполнено(ИНН) Тогда
		  Возврат Истина;
	  КонецЕсли;
	  
	  
	  //Если ИНН заполнен то проверем длину ИНН и сравниваем с корректной длиной
	  Если СтрДлина(ИНН) <> КорректнаяДлинаИНН(ТипКонтрагента) Тогда
		  ТекстОшибки = "Введен ИНН неверной длины";
		  Возврат Ложь;
	  КонецЕсли;
	  
	  //проверим запрещенные символы
	  Если ЕстьЗапрещенныеСимволы (ИНН) Тогда
		  ТекстОшибки = "Введен ИНН с неверными символами"; 
		  Возврат Ложь; 
	  КонецЕсли;
	  
	  //проверим правильность ИНН для юр.лиц
	  Если СтрДлина(ИНН) = 10 Тогда
		  
		  Если Не ПроверкаКонтрольногоРазряда10(ИНН) Тогда //вернет Истину или Ложь 
			  ТекстОшибки = "Введен несуществующий ИНН";
			  Возврат Ложь;
		  КонецЕсли;
		  
		  //проверим правильность ИНН для физ.лиц
	  Иначе
		  Если Не ПроверкаКонтрольногоРазряда12(ИНН) Тогда  //вернет Истину или Ложь
			  ТекстОшибки = "Введен несуществующий ИНН";
			  Возврат Ложь;
		  КонецЕсли;
		  
	  КонецЕсли;
	  
	  Возврат Истина;
	  
  КонецФункции
  
  
  Функция  КорректнаяДлинаИНН (ТипКонтрагента)
	  
	  Если ТипКонтрагента = ПредопределенноеЗначение("Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо") ТОгда
		  Возврат 10;  
	  Иначе
		  Возврат 12;  
	  КонецЕсли;
	  
  КонецФункции  
  
  Функция  ЕстьЗапрещенныеСимволы (ИНН)
	  
	  //проверка состава строки ИНН и КПП, чтобы там содержались только цифры
	  
	  //определим строку. состоящую только из цифр чтобы потом среди этого набора цифр искать символ из поля ИНН и КПП
	  РазрешенныеСимволы = "1234567890";
	  
	  //определим длину поля ИНН чтобы ограничить счетчик количеством символов
	  ДлинаИНН = СтрДлина(ИНН);
	  
	  //цикл со счетчиком для перебора символов для ИНН
	  Для Счетчик = 1 по ДлинаИНН Цикл
		  //получение очередного символа Сред()
		  СимволСтрокиИНН = Сред (ИНН, Счетчик, 1); 
		  
		  //определим есть ли каждый символ строки в числе разрешенных символов с помощью СтрНайти
		  //если найден запрещенный символ, функция вернет Истина и прервется цикл 
		  Если Не СтрНайти(РазрешенныеСимволы, СимволСтрокиИНН) Тогда
			  Возврат Истина;
			  Прервать; //прерываем цикл чтобы не проверять другие значения  
		  КонецЕсли;
		  
	  КонецЦикла;  
	  
	  Возврат Ложь; //нет запрещенных символов 
	  
  КонецФункции

  Функция ПроверкаКонтрольногоРазряда10(Знач ИНН)
	  
	  Перем КонтрольнаяСумма, КонтрольныйРазряд, Множители, ПроверкаКонтрольногоРазряда10ЗначногоИНН, Произведение, Сч, ЦифраИНН;
	  
	  //создадим массив с множителями
	  Множители = СтрРазделить("2,4,10,3,5,9,4,6,8,0", ",");
	  
	  //Вычисляется контрольная сумма со следующими весовыми коэффициентами разрядов 
	  //кода ИНН: (2,4,10,3,5,9,4,6,8,0)
	  КонтрольнаяСумма = 0;
	  
	  Для Сч = 1 по 10 Цикл 
		  ЦифраИНН = Число (Сред (ИНН, сч, 1));
		  Произведение = ЦифраИНН * Множители [сч-1];
		  КонтрольнаяСумма = КонтрольнаяСумма + Произведение;
	  КонецЦикла;  
	  
	  //Вычисляется контрольное число как остаток от деления контрольной суммы на 11
	  КонтрольныйРазряд = КонтрольнаяСумма % 11;
	  
	  //Если контрольное число больше 9, 
	  //то контрольное число вычисляется как остаток от деления контрольного числа на 10
	  Если КонтрольныйРазряд = 10 Тогда
		  КонтрольныйРазряд = 0;
	  КонецЕсли;
	  
	  //Контрольное число проверяется с десятым знаком ИНН. 
	  //В случае их равенства ИНН считается правильным
	  Если Сред (ИНН, 10, 1) <> Строка (КонтрольныйРазряд) Тогда  
		  ПроверкаКонтрольногоРазряда10ЗначногоИНН = Ложь; 
		  
	  Иначе
		  ПроверкаКонтрольногоРазряда10ЗначногоИНН = Истина;
		  
	  КонецЕсли;
	  
	  Возврат ПроверкаКонтрольногоРазряда10ЗначногоИНН;
	  
  КонецФункции 

  Функция ПроверкаКонтрольногоРазряда12(Знач ИНН)
	  
	  Перем КонтрольнаяСумма1, КонтрольныйРазряд1, КонтрольнаяСумма2, КонтрольныйРазряд2, Множители, ПроверкаКонтрольногоРазряда12ЗначногоИНН, Произведение, Сч, ЦифраИНН;
	  
	  //создадим массив с множителями
	  Множители = СтрРазделить("7,2,4,10,3,5,9,4,6,8,0", ",");
	  
	  //Вычисляется контрольная сумма по первым 11-ти знакам 
	  //со следующими весовыми коэффициентами: (7,2,4,10,3,5,9,4,6,8,0)
	  КонтрольнаяСумма1 = 0;
	  
	  Для Сч = 1 по 11 Цикл 
		  ЦифраИНН = Число (Сред (ИНН, сч, 1));
		  Произведение = ЦифраИНН * Множители [сч-1];
		  КонтрольнаяСумма1 = КонтрольнаяСумма1 + Произведение;
		  
	  КонецЦикла;
	  
	  //Вычисляется контрольное число_1 как остаток от деления контрольной суммы на 11
	  КонтрольныйРазряд1 = КонтрольнаяСумма1 % 11;
	  
	  //Если контрольное число_1 больше 9, 
	  //то контрольное число_1 вычисляется как остаток от деления контрольного числа_1 на 10
	  Если КонтрольныйРазряд1 = 10 Тогда
		  КонтрольныйРазряд1 = КонтрольныйРазряд1 %10;
	  КонецЕсли; 
	  
	  //Вычисляется контрольная сумма по 12-ти знакам со 
	  //следующими весовыми коэффициентами: (3,7,2,4,10,3,5,9,4,6,8,0)
	  //создадим массив с множителями
	  Множители = СтрРазделить("3,7,2,4,10,3,5,9,4,6,8,0", ",");
	  
	  КонтрольнаяСумма2 = 0;
	  
	  Для Сч = 1 по 12 Цикл 
		  ЦифраИНН = Число (Сред (ИНН, сч, 1));
		  Произведение = ЦифраИНН * Множители [сч-1];
		  КонтрольнаяСумма2 = КонтрольнаяСумма2 + Произведение;
		  
	  КонецЦикла; 
	  
	  //Вычисляется контрольное число_2 как остаток от деления контрольной суммы на 11
	  КонтрольныйРазряд2 = КонтрольнаяСумма2 % 11;
	  
	  //Если контрольное число_2 больше 9, то контрольное число_2 
	  //вычисляется как остаток от деления контрольного числа_2 на 10.
	  Если КонтрольныйРазряд2 = 10 Тогда
		  КонтрольныйРазряд2 = КонтрольныйРазряд2 %10;
	  КонецЕсли;
	  
	  //Контрольное число_1 проверяется с одиннадцатым знаком ИНН и 
	  //контрольное число_2 проверяется с двенадцатым знаком ИНН. 
	  //В случае их равенства ИНН считается правильным
	  Если Сред (ИНН, 11, 1) <> Строка (КонтрольныйРазряд1) Или Сред (ИНН, 12, 1) <> Строка (КонтрольныйРазряд2) Тогда 
		  ПроверкаКонтрольногоРазряда12ЗначногоИНН = Ложь; 
	  Иначе
		  ПроверкаКонтрольногоРазряда12ЗначногоИНН = Истина;
	  КонецЕсли;
	  
	  Возврат ПроверкаКонтрольногоРазряда12ЗначногоИНН;
	  
  КонецФункции 
Второй интересной задачей тут посчитала создание формы Админиистратора элемента Справочника "Сотрудники". На форме, помимо реквизитов сотрудника, должен быть флажок «Вход разрешён» и поля ввода «Имя для входа» и «Пароль», отражающие свойства пользователя ИБ.
При записи сотрудника из этой формы при необходимости должно выполняться:

создание пользователя ИБ с ролью «БазовыеПрава»;
изменение его пароля и имени для входа;
при снятии флажка — отключение стандартной аутентификации.
Рядом с полем ввода пароля должна быть команда «Случайный пароль», генерирующая случайный пароль и показывающая его пользователю.
В пользовательском режиме форма элемента выглядит так: image

Ниже приведу кодя модуля ФормыАдминистратора:

&НаКлиенте
Процедура СлучайныйПароль(Команда)
	//создать случайный пароль из символов
	СлучайныйПароль = Новый ГенераторСлучайныхЧисел;
	СимволыПароля = "1234567890_!№%:?*()-qwertyuiop[]asdfghjkl;'zxcvbnm,./QWERTYUIOP{}ASDFGHJKLZXCVBNM<>@";
	КоличествоСимволов = СтрДлина(СимволыПароля);
	Пароль = "";  
	
	Для Сч = 1 по 6 цикл
		Пароль = Пароль + Сред(СимволыПароля, СлучайныйПароль.СлучайноеЧисло(1,КоличествоСимволов), 1);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьПароль(Команда)
	//отключение у поля ввода «Пароль» режим пароля, чтобы пользователь увидел его и мог скопировать
	Элементы.Пароль.РежимПароля = Не Элементы.Пароль.РежимПароля 
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	//ищем пользователя по УИН - получим либо УИН, либо пустой
    Пользователь = ПользователиИнформационнойБазы.НайтиПоУникальномуИдентификатору(ТекущийОбъект.ИдентификаторПользователяИнформационнойБазы);
	//при записи нового сотрудника если у него вход разрешен и заполнено имя пользователя, 
	//то создаем пользователя
	Если Пользователь = Неопределено И ВходРазрешен И ЗначениеЗаполнено(ИмяДляВхода) Тогда 
		НовыйПользователь = СоздатьНовогоПользователя(ТекущийОбъект);
		
		//После создания пользователя ИБ присвоим его идентификатор реквизиту «ИдентификаторПользователяИБ» 
		//записываемого объекта, чтобы найти этого пользователя ИБ при следующем открытии формы.
		ТекущийОбъект.ИдентификаторПользователяИнформационнойБазы = НовыйПользователь.УникальныйИдентификатор;
		//смогла найти пользователя
	ИначеЕсли Пользователь <> Неопределено Тогда
        Пользователь.Имя = ИмяДляВхода;
		Пользователь.ПолноеИмя = ТекущийОбъект.Наименование; 
		Если Пароль <> "Установлен" Тогда
			//пропишем пароль
			Пользователь.Пароль = Пароль;
		КонецЕсли;   //проверить это можно только на коммерческой версии
		
		Пользователь.АутентификацияСтандартная = ВходРазрешен;  
		
		//чтобы данные которые изменили для пользователя сохранились, надо этого пользователя записать 
		//УИН не поменяется
		Пользователь.Записать();
		
	КонецЕсли;
			
КонецПроцедуры

&НаСервере
Функция СоздатьНовогоПользователя(Знач ТекущийОбъект)
	
	Перем НовыйПользователь;
	
	НовыйПользователь =  ПользователиИнформационнойБазы.СоздатьПользователя();
	НовыйПользователь.АутентификацияСтандартная = ВходРазрешен;
	НовыйПользователь.Имя = ИмяДляВхода;
	НовыйПользователь.ПолноеИмя = ТекущийОбъект.Наименование; 
	НовыйПользователь.Пароль = Пароль;
	НовыйПользователь.Роли.Добавить(Метаданные.Роли.БазовыеПрава);
	
	НовыйПользователь.Записать();
	Возврат НовыйПользователь;

КонецФункции

&НаСервере  
//вызывается если считываем уже записанного сотрудника
Процедура ПриЧтенииНаСервере(ТекущийОбъект) 
	//при открытии сотрудника нужно чтобы реквизиты ВходРазрешен, имя, пароль были заполнены
	
	//Найдем пользователя ИБ по идентификатору — «ТекущийОбъект.ИдентификаторПользователяИБ»
	Если ЗначениеЗаполнено(ТекущийОбъект.ИдентификаторПользователяИнформационнойБазы) Тогда
		Пользователь = ПользователиИнформационнойБазы.НайтиПоУникальномуИдентификатору(ТекущийОбъект.ИдентификаторПользователяИнформационнойБазы);
		//найти по УИН может вернуть как пользователя, так и значение Неопределено	
		//Заполните по данным пользователя ИБ реквизиты формы «ИмяДляВхода» и «Пароль». 
		//Реквизит «ВходРазрешён» заполните по реквизиту пользователя ИБ «АутентификацияСтандартная»  
		Если Пользователь <> Неопределено Тогда 
			ИмяДляВхода = Пользователь.Имя; 
			//узнать заполнен пароль или нет, проверим поле
			Если Пользователь.ПарольУстановлен Тогда
				Пароль = "Установлен";  //иначе поле пустое
			КонецЕсли;
			
			ВходРазрешен = Пользователь.АутентификацияСтандартная;
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры
